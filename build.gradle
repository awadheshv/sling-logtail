buildscript {
  ext.kotlin_version = '1.1.51'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}
plugins {
  id 'org.dm.bundle' version '0.10.0'
}

apply plugin: 'kotlin'
apply plugin: 'org.dm.bundle'

group = 'org.apache.sling'
version = '1.0.0-SNAPSHOT'

description = """Apache Sling Log Tail Implementation"""

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
repositories {
  maven { url "http://repo.maven.apache.org/maven2" }
  mavenCentral()
}

dependencies {
  compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.3'
  compileOnly group: 'org.apache.sling', name: 'org.apache.sling.api', version: '2.16.2'
  compileOnly group: 'org.apache.sling', name: 'org.apache.sling.commons.log', version: '4.0.0'
  compileOnly group: 'biz.aQute.bnd', name: 'bnd', version: '2.1.0'
  compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
  compileOnly group: 'org.osgi', name: 'org.osgi.core', version: '4.2.0'
  compileOnly group: 'org.osgi', name: 'org.osgi.compendium', version: '4.2.0'
  compileOnly group: 'org.osgi', name: 'osgi.annotation', version: '6.0.1'
  compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.3'
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

jar {
  //comment this if kotlin runtime is available separately on your environment
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

bundle {
  instructions << [
    'Bundle-SymbolicName'   : project.name,
    'Bundle-Name'           : project.name,
    'Built-By'              : System.getProperty('user.name'),
    'Built-Date'            : new Date(),
    'Built-JDK'             : System.getProperty('java.version'),
    'Built-Gradle'          : gradle.gradleVersion,
    'Sling-Bundle-Resources': '/libs/sling/logtail-plugin/css,/libs/sling/logtail-plugin/js'
  ]
}

